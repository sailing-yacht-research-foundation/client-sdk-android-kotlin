apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Sailing Yacht Research Foundation Official Android SDK'
                    url = 'https://github.com/getstream/stream-chat-android'
                    licenses {
                        license {
                            name = 'Stream License'
                            url = 'https://github.com/sailing-yacht-research-foundation/client-sdk-android-kotlin/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'pavel-corshagin'
                            name = 'Hung Nguyen'
                            email = 'hung.nguyen@scopicsoftware.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/sailing-yacht-research-foundation/client-sdk-android-kotlin.git'
                        developerConnection = 'scm:git:ssh://github.com/sailing-yacht-research-foundation/client-sdk-android-kotlin.git'
                        url = 'https://github.com/sailing-yacht-research-foundation/client-sdk-android-kotlin/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}